# cmake -DCMAKE_INSTALL_PREFIX=<your installation dir> <path>
CMAKE_MINIMUM_REQUIRED(VERSION 2.4)
SET(CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake)

# CMP0011 policy before first include
# others policies are set in SiconosProject
IF(POLICY CMP0011)
  CMAKE_POLICY(SET CMP0011 NEW)
ENDIF(POLICY CMP0011)
INCLUDE(SiconosProject)

ENABLE_LANGUAGE(C)
ENABLE_LANGUAGE(CXX)
ENABLE_LANGUAGE(Fortran) 

SET(EXAMPLES_DIRECTORIES
  Control
  Electronics
  Mechanics
  Numerics
  Robotics/HuMAns_pa10
)

SET(NO_TEST_FILES QGL)

SICONOS_PROJECT(Examples 3 0 0)

COMPILE_WITH(SiconosNumerics REQUIRED)
COMPILE_WITH(SiconosKernel REQUIRED)

#
# Internals
#

SET(CMAKE_SICONOS_COMPILE_OBJECT "${CMAKE_INSTALL_PREFIX}/bin/siconos --noexec <SOURCE>")
SET(CMAKE_SICONOS_LINK_EXECUTABLE "")

ENABLE_TESTING()

MACRO(ADD_EXAMPLE _N _EX)
  MESSAGE("Adding example ${_N} (${_EX})")
  ADD_TEST(${_N} ${_EX})
  SET_TESTS_PROPERTIES(${_N} PROPERTIES FAIL_REGULAR_EXPRESSION "FAILURE;Exception;failed")
ENDMACRO(ADD_EXAMPLE _N _EX)

FOREACH(_D ${EXAMPLES_DIRECTORIES})
  FILE(GLOB EXAMPLES_P ${CMAKE_SOURCE_DIR}/${_D}/*/*.cpp )
  IF(EXAMPLES_P)
    FOREACH(_P ${EXAMPLES_P})
      GET_FILENAME_COMPONENT(_RP ${_P} ABSOLUTE)
      LIST(APPEND EXAMPLES ${_RP})
    ENDFOREACH(_P ${EXAMPLES_P})
  ENDIF(EXAMPLES_P)
ENDFOREACH(_D ${EXAMPLES_DIRECTORIES})

FOREACH(EXAMPLE ${EXAMPLES})
  IF(NOT EXAMPLE MATCHES Plugin)
    SET(TEST_ME TRUE)
    FOREACH(_NT ${NO_TEST_FILES})
      IF(EXAMPLE MATCHES "${_NT}")
        SET(TEST_ME FALSE)
      ENDIF(EXAMPLE MATCHES "${_NT}")
    ENDFOREACH(_NT ${NO_TEST_FILES})
    IF(TEST_ME)
      GET_FILENAME_COMPONENT(EXAMPLE_NAME ${EXAMPLE} NAME_WE)
      SET_SOURCE_FILES_PROPERTIES(${EXAMPLE} PROPERTIES LANGUAGE SICONOS)
      ADD_EXECUTABLE(${EXAMPLE_NAME} ${EXAMPLE})
      ADD_EXAMPLE(${EXAMPLE_NAME} ${EXAMPLE_NAME})
    ENDIF(TEST_ME)
  ENDIF(NOT EXAMPLE MATCHES Plugin)
ENDFOREACH(EXAMPLE ${EXAMPLES})
